# # Barrier certificates for collision avoidance

# **Adapted from**: Example 2 of "Engineering applications of SOS polynomials" by Georgina Hall, from 
# the 2019 AMS Short Course on Sum of Squares: Theory and Applications
# Implementation by: Hugo Tadashi Kussaba

using Test #src
using DynamicPolynomials
@polyvar x[1:2]

using SumOfSquares
using CSDP

# We need to pick an SDP solver, see [here](https://jump.dev/JuMP.jl/v1.8/installation/#Supported-solvers) for a list of the available choices.
# We use `SOSModel` instead of `Model` to be able to use the `>=` syntax for Sum-of-Squares constraints.
solver = optimizer_with_attributes(CSDP.Optimizer, MOI.Silent() => true)
model = SOSModel(solver);

# We define below the vector field ``\text{d}x/\text{d}t = f``

f = [ x[2],
     -x[1] + (1/3)*x[1]^3 - x[2]]

# Semi-algebraic function describing the unsafe set ùí≥·µ§
g‚ÇÅ = -(x[1]+1)^2 - (x[2]+1)^2 + 0.16  # ùí≥·µ§ = {x ‚àà R¬≤: g‚ÇÅ(x) ‚â• 0}
# Semi-algebraic function describing the initial set ùí≥‚ÇÄ
h‚ÇÅ = -(x[1]-1.5)^2 - x[2]^2 + 0.25    # ùí≥‚ÇÄ = {x ‚àà R¬≤: h‚ÇÅ(x) ‚â• 0}

# Define SOS barrier function B
monos = monomials(x, 0:4)
@variable(model, B, Poly(monos))

# Define barrier certificate constraints using SOS relaxation

# B(x) > 0 for all x ‚àà ùí≥·µ§
Œµ = 0.001
@constraint(model, B >= Œµ, domain = @set(g‚ÇÅ >= 0))

# B(x) ‚â§ 0 for all x ‚àà ùí≥‚ÇÄ
@constraint(model, B <= 0, domain = @set(h‚ÇÅ >= 0))

# ·∏Ç(x) ‚â§ 0 for all x ‚àà R¬≤
using LinearAlgebra # Needed for `dot`
dBdt = dot(differentiate(B, x), f)
@constraint(model, -dBdt >= 0)

# The model is ready to be optimized by the solver:
JuMP.optimize!(model)

# We verify that the solver has found a feasible solution:
JuMP.primal_status(model)
@test JuMP.primal_status(model) == MOI.FEASIBLE_POINT #src

# Plot the phase plot with the 0-level set of the barrier function, and the boundary of the initial and unsafe sets
import DifferentialEquations, Plots, ImplicitPlots
function phase_plot(f, B, g‚ÇÅ, h‚ÇÅ, quiver_scaling, Œît, X0, solver = DifferentialEquations.Tsit5())
    p = ImplicitPlots.implicit_plot(B*g‚ÇÅ*h‚ÇÅ; xlims=(-2, 3), ylims=(-2.5, 2.5), resolution = 1000, label="")
    Plots.plot(p)
    ‚àá(vx, vy) = [fi(x[1] => vx, x[2] => vy) for fi in f]
    ‚àápt(v, p, t) = ‚àá(v[1], v[2])
    function traj(v0)
        tspan = (0.0, Œît)
        prob = DifferentialEquations.ODEProblem(‚àápt, v0, tspan)
        return DifferentialEquations.solve(prob, solver, reltol=1e-8, abstol=1e-8)
    end
    ticks = -5:0.5:5
    X = repeat(ticks, 1, length(ticks))
    Y = X'
    Plots.quiver!(X, Y, quiver = (x, y) -> ‚àá(x, y) / quiver_scaling, linewidth=0.5)
    for x0 in X0
        Plots.plot!(traj(x0), vars=(1, 2), label = nothing)
    end
    Plots.plot!(xlims = (-2, 3), ylims = (-2.5, 2.5))
end 

phase_plot(f, value(B), g‚ÇÅ, h‚ÇÅ, 10, 30.0, [[x1, x2] for x1 in 1.2:0.2:1.7, x2 in -0.35:0.1:0.35])